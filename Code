#include <Arduino.h>
#include <SPI.h>
#include <MFRC522.h>
#include <Wire.h> 
#include <LiquidCrystal_I2C.h>
#include <Keypad.h>
#include <Servo.h>

#define RST_PIN 9
#define SS_PIN 10
#define SERVO_PIN 3
#define BUZZER_PIN 8
#define LED_YESIL 2

MFRC522 mfrc522(SS_PIN, RST_PIN); 
LiquidCrystal_I2C lcd(0x27, 16, 2);
Servo myServo;

// KEYPAD
const byte SATIR = 4; 
const byte SUTUN = 4; 
char tuslar[SATIR][SUTUN] = {
  {'1','2','3','A'},
  {'4','5','6','B'},
  {'7','8','9','C'},
  {'*','0','#','D'}
};
// Bağlantı: Soldan sağa 1-4 Dijital, 5-8 Analog
byte satirPinleri[SATIR] = {7, 6, 5, 4}; 
byte sutunPinleri[SUTUN] = {A0, A1, A2, A3}; 
Keypad keypad = Keypad(makeKeymap(tuslar), satirPinleri, sutunPinleri, SATIR, SUTUN);

// GÜVENLİK
String kendiKartID = "XX XX XX XX"; //Buraya kart ID'nizi yazin
String dogruSifre = "1479"; //Belirlediğiniz şifre
String girilenSifre = "";
bool kartOnaylandi = false;
int yanlisGirisSayaci = 0;

void karsilamaEkrani();
void kapiyiAc();
void sifirlama();
void sesCal(int tur); 
void hirsizAlarmi(); 

void setup() {
  Serial.begin(9600);
  SPI.begin();
  mfrc522.PCD_Init();
  
  lcd.init();      
  lcd.backlight();
  
  myServo.attach(SERVO_PIN);
  myServo.write(0);
  
  pinMode(BUZZER_PIN, OUTPUT);
  pinMode(LED_YESIL, OUTPUT);
  
  karsilamaEkrani();
}

void loop() {
  if (!kartOnaylandi) {
    if ( ! mfrc522.PICC_IsNewCardPresent()) return;
    if ( ! mfrc522.PICC_ReadCardSerial()) return;
    
    String okunanID = "";
    for (byte i = 0; i < mfrc522.uid.size; i++) {
       okunanID += String(mfrc522.uid.uidByte[i] < 0x10 ? " 0" : " ");
       okunanID += String(mfrc522.uid.uidByte[i], HEX);
    }
    okunanID.toUpperCase();
    okunanID.trim();

    // Buradan ID öğrenebiliriz
    Serial.print("Okunan ID: ");
    Serial.println(okunanID);

    if (okunanID == kendiKartID) {
      sesCal(4); 
      lcd.clear();
      lcd.print("Kart Dogru!");
      delay(1000);
      lcd.clear();
      lcd.print("Sifre Giriniz:");
      lcd.setCursor(0,1);
      kartOnaylandi = true; 
      yanlisGirisSayaci = 0; 
    } else {
      sesCal(3); 
      lcd.clear();
      lcd.print("Gecersiz Kart!");
      delay(2000);
      karsilamaEkrani();
    }
    mfrc522.PICC_HaltA();
  } 
  else {
    char tus = keypad.getKey();
    
    if (tus) {
      sesCal(1); 
      
      if (tus == '#') { // GİRİŞ TUŞU
        if (girilenSifre == dogruSifre) {
          kapiyiAc();
        } else {
          
          yanlisGirisSayaci++;
          lcd.clear();
          lcd.print("Yanlis Sifre!");
          lcd.setCursor(0,1);
          lcd.print("Deneme: ");
          lcd.print(yanlisGirisSayaci);
          lcd.print("/3");
          
          sesCal(3);
          delay(1500);
          
          if (yanlisGirisSayaci >= 3) {
            hirsizAlarmi();
          }
          
          // Sadece şifreyi temizle kartı unutma
          girilenSifre = ""; 
          lcd.clear();
          lcd.print("Tekrar Deneyin:");
          lcd.setCursor(0,1);
        }
      } 
      else if (tus == '*') { // SİLME TUŞU
        girilenSifre = "";
        lcd.setCursor(0, 1);
        lcd.print("                ");
        lcd.setCursor(0, 1);
      } 
      else {
        girilenSifre += tus;
        lcd.print("*");
      }
    }
  }
}


void karsilamaEkrani() {
  lcd.clear();
  lcd.setCursor(0,0);
  lcd.print("GUVENLI KASA");
  lcd.setCursor(0,1);
  lcd.print("Karti Okutunuz");
  digitalWrite(LED_YESIL, LOW);
  girilenSifre = "";
  kartOnaylandi = false;
}

void kapiyiAc() {
  lcd.clear();
  lcd.print("GIRIS BASARILI");
  lcd.setCursor(0,1);
  lcd.print("Hosgeldiniz");
  
  digitalWrite(LED_YESIL, HIGH);
  sesCal(2); 
  
  myServo.write(90); // Kapıyı aç
  delay(5000); // 5 saniye bekle
  
  myServo.write(0); // Kapıyı kapat
  digitalWrite(LED_YESIL, LOW);
  yanlisGirisSayaci = 0;
  sifirlama();
}

void sifirlama() {
  girilenSifre = "";
  kartOnaylandi = false;
  karsilamaEkrani();
}

void hirsizAlarmi() {
  lcd.clear();
  lcd.print("!! ALARM !!");
  lcd.setCursor(0,1);
  lcd.print("YETKISIZ GIRIS");
  
  digitalWrite(LED_YESIL, LOW); 

  while(true) { // Sonsuz döngü
    lcd.noBacklight(); 
    tone(BUZZER_PIN, 1000); 
    delay(200);
    
    lcd.backlight();
    tone(BUZZER_PIN, 2000); 
    delay(200);
  }
}

void sesCal(int tur) {
  if (tur == 1) { tone(BUZZER_PIN, 1500, 50); } // Tuş
  else if (tur == 2) { 
    tone(BUZZER_PIN, 1000); delay(100);
    tone(BUZZER_PIN, 1500); delay(100);
    tone(BUZZER_PIN, 2000); delay(200); noTone(BUZZER_PIN);
  } 
  else if (tur == 3) {
    tone(BUZZER_PIN, 200, 300); delay(300);
    tone(BUZZER_PIN, 150, 400); noTone(BUZZER_PIN);
  } 
  else if (tur == 4) {
    tone(BUZZER_PIN, 2000, 100); delay(100);
    tone(BUZZER_PIN, 2500, 100); 
  }
}
